// Top-level build file where you can add configuration options common to all sub-projects/modules

plugins {
    id 'com.android.application' version '7.3.0' apply false
    id 'com.android.library' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.23.1'
    id 'org.jetbrains.kotlin.jvm' version '1.7.10' apply false
    id 'com.google.devtools.ksp' version '1.9.0-1.0.12' apply false
    id 'androidx.navigation.safeargs' version '2.5.0' apply false
    id 'com.google.dagger.hilt.android' version '2.48' apply false
    id 'com.google.gms.google-services' version '4.3.15' apply false
}

def projectSource = file(projectDir)
def configFile = files("$rootDir/config/detekt/detekt.yml")
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

apply plugin: 'io.gitlab.arturbosch.detekt'

tasks.register("detektAll", io.gitlab.arturbosch.detekt.Detekt) {
    def autoFix = project.hasProperty('detektAutoFix')

    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = autoFix
    buildUponDefaultConfig = true
    setSource(projectSource)
    config.setFrom(configFile)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.23.1"
}